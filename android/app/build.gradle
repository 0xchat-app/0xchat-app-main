plugins {
  id 'com.android.application'
  id 'com.google.gms.google-services'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
  localPropertiesFile.withReader('UTF-8') { reader ->
    localProperties.load(reader)
  }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
  throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
  flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
  flutterVersionName = '1.0'
}

apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
  namespace 'com.oxchat.nostr'
  buildFeatures {
    buildConfig true
  }
  compileSdk 34
  buildToolsVersion "34.0.0"
  ndkVersion "25.1.8937393"

  //disable resource file check
  def version = parsePubspecVersion()
  def versionParts = version.split("\\+")
  defaultConfig {
    applicationId "com.oxchat.nostr"
    minSdk 29
    targetSdk 34
    multiDexEnabled true
    versionCode versionParts[1] as Integer
    versionName versionParts[0]

    ndk {
      abiFilters 'armeabi', "armeabi-v7a", 'arm64-v8a'
      // can also add 'x86', 'x86_64', 'mips', 'mips64', 'armeabi-v7a', 'arm64-v8a'
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    debug {
      ndk {
        abiFilters 'armeabi', 'x86', 'x86_64', 'armeabi-v7a'
        // can also add 'x86', 'x86_64', 'mips', 'mips64', 'armeabi-v7a', 'arm64-v8a'
      }
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  lint {
    abortOnError false
    checkReleaseBuilds false
  }
}

flutter {
  source '../..'
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation 'androidx.appcompat:appcompat:1.2.0'
  implementation 'androidx.multidex:multidex:2.0.1'
  implementation platform('com.google.firebase:firebase-bom:32.0.0')
}

def parsePubspecVersion() {
  def pubspec = file('../../pubspec.yaml')
  def version = '1.0.0+1' // Default value

  if (pubspec.exists()) {
    def contents = pubspec.readLines()
    def versionLine = contents.find { it.startsWith('version:') }
    if (versionLine != null) {
      version = versionLine.split(' ')[1].trim()
    }
  }

  return version
}
